/*
 * Copyright 2009 Leonard Axelsson
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

configurations { 
  standalone
}

repositories {
  mavenCentral()
}

dependencies {
  standalone project(':starter')
  standalone 'org.mortbay.jetty:jetty-embedded:6.1.21'
  standalone 'org.mortbay.jetty:jetty-naming:6.1.21'
  standalone 'org.mortbay.jetty:jetty-plus:6.1.21'
}

defaultTasks 'buildStandaloneWar'

def depsDir = file('build/dependencies')
def outDir = file('build/out')
def warDir = file('build/explodedWar')

task extractDependencies(dependsOn: ':starter:jar') << {
  for(file in configurations.standalone) {
    ant.unzip(src: file.absolutePath,
              dest: depsDir)
  }
	
  ant.delete(dir: new File(depsDir, 'META-INF'))
}

task buildStandaloneWar(dependsOn: extractDependencies) << {
  if(project.hasProperty('inWar') && !project.hasProperty('outWar')) {
    def warName = inWar[0..(inWar.indexOf('.war')-1)]
    outWar = "build/$warName-standalone.war"
  }

  ant.unzip(src: inWar, dest: warDir)
	
  copy {
    from depsDir
    from outDir
    into warDir
  }

  ant.manifest(file: new File(warDir, 'META-INF/MANIFEST.MF')) {
    attribute(name: 'Main-Class', value: 'com.xlson.standalonewar.Starter')
  }

  ant.zip(destfile: "$outWar") {
    fileset(dir: warDir)
  }
}
